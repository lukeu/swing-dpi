plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
}

sourceCompatibility = 1.6
targetCompatibility = 1.6

group = 'com.github.lukeu'
version = '0.6'

// Currently no dependencies other than the JDK
repositories {
}

dependencies {
}

// ---=== | JDK 6 support | ===---

// Reference:
//  https://docs.gradle.org/3.0/release-notes.html#running-gradle-on-java-6-is-no-longer-supported

assert hasProperty('java6Home') : "Set 'java6Home' in gradle.properties (maybe in GRADLE_USER_HOME)"
def javaExecutablesPath = new File(java6Home, 'bin')
def javaExecutables = [:].withDefault { execName ->
    def executable = new File(javaExecutablesPath, execName)
    if (!executable.exists()) {
        executable = new File(javaExecutablesPath, execName + '.exe')
    }
    assert executable.exists() : "There is no ${execName}(.exe) in ${javaExecutablesPath}"
    executable
}
tasks.withType(AbstractCompile) {
    options.with {
        fork = true
        forkOptions.executable = javaExecutables.javac
    }
}
tasks.withType(Javadoc) {
    executable = javaExecutables.javadoc
}
tasks.withType(Test) {
    executable = javaExecutables.java
}
tasks.withType(JavaExec) {
    executable = javaExecutables.java
}


// ---=== | Publishing | ===---

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
            pom {
                name = 'swing-dpi'
                description = 'HiDPI and UI-scaling support for Java-Swing via the PLaF mechanism'
                url = 'https://github.com/lukeu/swing-dpi'
                licenses {
                    license {
                        name = 'GNU Lesser General Public License v2.1'
                        url = 'https://www.gnu.org/licenses/old-licenses/lgpl-2.1.txt'
                    }
                }
                developers {
                    developer {
                        id = 'lukeu'
                        name = 'Luke Usherwood'
                        email = 'ldubox-coding101@yahoo.com'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/lukeu/swing-dpi.git'
                    developerConnection = 'scm:git:git://github.com/lukeu/swing-dpi.git'
                    url = 'https://github.com/lukeu/swing-dpi/tree/master'
                }
            }
        }
    }
    repositories {
        maven {
            if (project.hasProperty('release')) {
                url = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
                credentials {
                    username = ossrhUser
                    password = ossrhPassword
                }
            } else {
                url = "$buildDir/repos/snapshots"
            }
        }
        mavenLocal()
    }
    signing {
        sign publishing.publications.mavenJava
    }
}
